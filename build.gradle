
plugins {
    id 'java-library'
    id 'com.adarshr.test-logger' version '1.7.0'
    id 'org.asciidoctor.convert' version '1.5.9.2'
    id 'net.ltgt.apt-eclipse' version '0.21'
}

repositories {
  mavenCentral()
}

def cucumberVersion = '4.7.1'
def junitVersion = '5.5.2'
def structurizrVersion = '1.3.0'
def immutablesVersion = '2.7.5'

dependencies {
  // Structurizr
  api "com.structurizr:structurizr-core:${structurizrVersion}"
  implementation "com.structurizr:structurizr-spring:${structurizrVersion}"
  implementation "com.structurizr:structurizr-plantuml:${structurizrVersion}"

  // Utilities
  implementation 'com.google.guava:guava:27.0-jre'
  implementation 'org.apache.commons:commons-lang3:3.9'
  
  // Immutables
  annotationProcessor "org.immutables:value:${immutablesVersion}"
  compileOnly "org.immutables:value-annotations:${immutablesVersion}"

  // Unit testing
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:${junitVersion}"
  testImplementation 'org.assertj:assertj-core:3.13.2'
  testImplementation 'org.mockito:mockito-core:3.0.0'
  
  // Cucumber
  testImplementation "io.cucumber:cucumber-java:${cucumberVersion}"
  testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
  testImplementation "io.cucumber:cucumber-picocontainer:${cucumberVersion}"
  testImplementation 'org.slf4j:slf4j-log4j12:1.7.28'
  testRuntimeOnly files("${System.getProperty('java.home')}/../lib/tools.jar")

  // Asciidoctor Plugin
  asciidoctor 'org.asciidoctor:asciidoctorj-diagram:1.5.18'
}

// Configure Asciidoctor
asciidoctor {
  requires 'asciidoctor-diagram'
}

// Configure JUnit
test {
  useJUnitPlatform()
}
testlogger {
    showStandardStreams true
}

// Configure Cucumber
configurations {
    cucumberRuntime {
        extendsFrom testImplementation, testRuntimeOnly
    }
}
task cucumber() {
  description 'Runs the Cucumber tests'
  dependsOn processTestResources, compileTestJava
  group 'verification'
  doLast {
    javaexec {
      main = "io.cucumber.core.cli.Main"
      classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', 'pretty', '--glue', 'com.krloxz.archidocs.test', 'src/test/resources']
    }
  }
}
